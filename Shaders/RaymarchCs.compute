#pragma kernel CSMain

#include "Packages/com.davidkimighty.rayman/Shaders/Library/Core/Math.hlsl"
#include "Packages/com.davidkimighty.rayman/Shaders/Library/Core/SDF.hlsl"
#include "Packages/com.davidkimighty.rayman/Shaders/Library/Core/Operation.hlsl"
#include "Packages/com.davidkimighty.rayman/Shaders/Library/Core/Raymarch.hlsl"
#include "Packages/com.davidkimighty.rayman/Shaders/Library/Core/BVH.hlsl"

struct Shape
{
    int groupId;
    int id;
    float4x4 transform;
    float3 lossyScale;
    int type;
    float3 size;
    float roundness;
    int combination;
    float smoothness;
    float4 color;
    float4 emissionColor;
    float emissionIntensity;
};

struct RaymarchResult
{
    float3 hitPoint;
    float travelDistance;
    float lastHitDistance;
    float3 rayDirection;
    float4 color;
    float3 normal;
};

float screenWidth;
float screenHeight;
float4 cameraPosition;
float4x4 cameraToWorld;
float4x4 inverseProjectionMatrix;
float4x4 viewProjectionMatrix;

int maxSteps;
int maxDistance;
int shapeCount;
StructuredBuffer<Shape> shapeBuffer;

int hitCount = 0;
int hitIds[RAY_MAX_HITS];

RWStructuredBuffer<RaymarchResult> resultBuffer;
float4 color;

inline float BlendDistance(inout float totalDist, const float3 pos, const Shape shape)
{
    float dist = GetShapeSDF(pos, shape.type, shape.size, shape.roundness);
    float blend = 0;
    totalDist = CombineShapes(totalDist, dist, shape.combination, shape.smoothness, blend);
    return blend;
}

inline float Map(inout Ray ray)
{
    float totalDist = maxDistance;
    float4 localColor = shapeBuffer[0].color;
    
    for (int i = 0; i < shapeCount; i++)
    {
        Shape shape = shapeBuffer[i];
        float3 pos = NormalizeScale(ApplyMatrix(ray.hitPoint, shape.transform), shape.lossyScale);
        float blend = BlendDistance(totalDist, pos, shape);
        localColor = lerp(localColor, shape.color + shape.emissionColor * shape.emissionIntensity, blend);
    }
    color = localColor;
    return totalDist;
}

inline float NormalMap(const float3 rayPos)
{
    float totalDist = maxDistance;
    
    for (int i = 0; i < shapeCount; i++)
    {
        Shape shape = shapeBuffer[i];
        float3 pos = NormalizeScale(ApplyMatrix(rayPos, shape.transform), shape.lossyScale);
        BlendDistance(totalDist, pos, shape);
    }
    return totalDist;
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    const float2 uv = float2(id.xy) / float2(screenWidth, screenHeight);
    const float2 ndc = uv * 2.0 - 1.0;

    const float4 posCS = float4(ndc, 0.0, 1.0);
    float4 posVS = mul(inverseProjectionMatrix, posCS);
    posVS /= posVS.w;
    
    float4 posWS = mul(cameraToWorld, posVS);
    float3 rayDirWS = normalize(posWS.xyz - cameraPosition.xyz);
    Ray ray = CreateRay(posWS.xyz, rayDirWS, maxSteps, maxDistance);

    Raymarch(ray);
    
    RaymarchResult result = (RaymarchResult)0;
    result.hitPoint = ray.hitPoint;
    result.travelDistance = ray.travelDistance;
    result.lastHitDistance = ray.lastHitDistance;
    result.rayDirection = ray.dir;
    result.color = color;
    result.normal = GetNormal(ray.hitPoint);
    
    uint index = id.y * screenWidth + id.x;
    resultBuffer[index] = result;
}
