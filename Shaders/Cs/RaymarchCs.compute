#pragma kernel CSMain
#pragma target 5.0

#include "Packages/com.davidkimighty.rayman/Shaders/Library/Core/Math.hlsl"
#include "Packages/com.davidkimighty.rayman/Shaders/Library/Core/SDF.hlsl"
#include "Packages/com.davidkimighty.rayman/Shaders/Library/Core/Operation.hlsl"
#include "Packages/com.davidkimighty.rayman/Shaders/Library/Core/Raymarch.hlsl"
#include "Packages/com.davidkimighty.rayman/Shaders/Library/Core/BVH.hlsl"

struct Shape
{
    int type;
    float4x4 transform;
    float3 size;
    float3 pivot;
    int operation;
    float smoothness;
    float roundness;
    half4 color;
    half4 emissionColor;
    float emissionIntensity;
    int distortionEnabled;
};

struct RaymarchResult
{
    float3 hitPoint;
    float travelDistance;
    float lastHitDistance;
    float3 rayDirection;
    float4 color;
    float3 normal;
};

float _ScreenWidth;
float _ScreenHeight;
float _RenderScale;
float4 _CameraPosition;
float4x4 _CameraToWorld;
float4x4 _InverseProjectionMatrix;
float4x4 _ViewProjectionMatrix;

int _MaxSteps;
float _MaxDistance;
StructuredBuffer<Shape> _ShapeBuffer;
StructuredBuffer<NodeAABB> _NodeBuffer;
RWStructuredBuffer<RaymarchResult> _ResultBuffer;

int2 hitCount; // x is leaf
int hitIds[RAY_MAX_HITS];
float4 color;

inline float Map(const Ray ray)
{
    float totalDist = _MaxDistance;
    float4 localColor =  _ShapeBuffer[hitIds[0]].color;
    
    for (int i = 0; i < hitCount.x; i++)
    {
        Shape shape = _ShapeBuffer[hitIds[i]];
        float3 pos = ApplyMatrix(ray.hitPoint, shape.transform);
        pos -= GetPivotOffset(shape.type, shape.pivot, shape.size);
        
        float3 scale = GetScale(shape.transform);
        float scaleFactor = min(scale.x, min(scale.y, scale.z));
        float dist = GetShapeSDF(pos, shape.type, shape.size, shape.roundness) / scaleFactor;
        
        float blend = 0;
        totalDist = CombineShapes(totalDist, dist, shape.operation, shape.smoothness, blend);
        localColor = lerp(localColor, shape.color + shape.emissionColor * shape.emissionIntensity, blend);
    }
    color = localColor;
    return totalDist;
}

inline float NormalMap(const float3 rayPos)
{
    float totalDist = _MaxDistance;
    
    for (int i = 0; i < hitCount.x; i++)
    {
        Shape shape = _ShapeBuffer[hitIds[i]];
        float3 pos = ApplyMatrix(rayPos, shape.transform);
        pos -= GetPivotOffset(shape.type, shape.pivot, shape.size);
        
        float3 scale = GetScale(shape.transform);
        float scaleFactor = min(scale.x, min(scale.y, scale.z));
        float dist = GetShapeSDF(pos, shape.type, shape.size, shape.roundness) / scaleFactor;
        
        float blend = 0;
        totalDist = CombineShapes(totalDist, dist, shape.operation, shape.smoothness, blend);
    }
    return totalDist;
}

inline NodeAABB GetNode(const int index)
{
    return _NodeBuffer[index];
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    const float2 uv = float2(id.xy) / (float2(_ScreenWidth, _ScreenHeight) * _RenderScale);
    const float2 ndc = uv * 2.0 - 1.0;

    const float4 posCS = float4(ndc, 0.0, 1.0);
    float4 posVS = mul(_InverseProjectionMatrix, posCS);
    posVS /= posVS.w;
    
    float4 posWS = mul(_CameraToWorld, posVS);
    float3 rayDirWS = normalize(posWS.xyz - _CameraPosition.xyz);
    Ray ray = CreateRay(posWS.xyz, rayDirWS, _MaxSteps, _MaxDistance);
    ray.travelDistance = length(ray.hitPoint - _CameraPosition.xyz);

    TraverseAabbTree(0, ray, hitIds, hitCount);
    InsertionSort(hitIds, hitCount.x);
    Raymarch(ray);
    
    RaymarchResult result = (RaymarchResult)0;
    result.hitPoint = ray.hitPoint;
    result.travelDistance = ray.travelDistance;
    result.lastHitDistance = ray.lastHitDistance;
    result.rayDirection = ray.dir;
    result.color = color;
    result.normal = GetNormal(ray.hitPoint);
    
    uint index = id.y * _ScreenWidth + id.x;
    _ResultBuffer[index] = result;
}
